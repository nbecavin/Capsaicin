/**********************************************************************
Copyright (c) 2022 Advanced Micro Devices, Inc. All rights reserved.

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.  IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.
********************************************************************/

float3 g_Eye;
float4x4 g_ViewProjectionInverse;

struct [raypayload] RayPayload
{
    float3 color : read(caller) : write(caller,closesthit,miss,anyhit);
};

RaytracingAccelerationStructure g_Scene;
RWTexture2D<float4> g_RenderTarget;
uint g_Offset;


TriangleHitGroup MyHitGroup =
{
    "", // AnyHit
    "MyClosestHitShader", // ClosestHit
};

TriangleHitGroup MyHitGroup2 =
{
    "", // AnyHit
    "MyClosestHitShader2", // ClosestHit
};

RaytracingShaderConfig MyShaderConfig =
{
    sizeof(RayPayload), // max payload size
    sizeof(BuiltInTriangleIntersectionAttributes) // max attribute size
};

RaytracingPipelineConfig MyPipelineConfig =
{
    1 // max trace recursion depth
};

[shader("raygeneration")]
void MyRaygenShader()
{
    float2 xy = DispatchRaysIndex().xy + 0.5f;
    float2 screen_pos = xy / DispatchRaysDimensions().xy * 2.0 - 1.0;
    screen_pos.y = -screen_pos.y;

    // Unproject the pixel coordinate into a ray.
    float4 world = mul(g_ViewProjectionInverse, float4(screen_pos, 0, 1));
    world.xyz /= world.w;

    RayDesc ray;
    ray.Origin = g_Eye;
    ray.Direction = normalize(world.xyz - g_Eye);
    ray.TMin = 0.001;
    ray.TMax = 10000.0;
    RayPayload payload = { float3(1.0f, 0.0f, 0.0f) };
    TraceRay(g_Scene, RAY_FLAG_CULL_BACK_FACING_TRIANGLES, ~0, 0, 1, 0, ray, payload);

    g_RenderTarget[DispatchRaysIndex().xy] = float4(payload.color, 1.0F);
}

uint hash(uint a)
{
    a = (a + 0x7ed55d16) + (a << 12);
    a = (a ^ 0xc761c23c) ^ (a >> 19);
    a = (a + 0x165667b1) + (a << 5);
    a = (a + 0xd3a2646c) ^ (a << 9);
    a = (a + 0xfd7046c5) + (a << 3);
    a = (a ^ 0xb55a4f09) ^ (a >> 16);
    return a;
}

[shader("closesthit")]
void MyClosestHitShader(inout RayPayload payload, in BuiltInTriangleIntersectionAttributes attr)
{
    uint mhash = hash(InstanceIndex() + GeometryIndex() + g_Offset);
    float3 hashColour = float3(float(mhash & 255), float((mhash >> 8) & 255), float((mhash >> 16) & 255)) / 255.0;
    payload.color = hashColour;
}

[shader("closesthit")]
void MyClosestHitShader2(inout RayPayload payload, in BuiltInTriangleIntersectionAttributes attr)
{
    uint mhash = hash(InstanceIndex() + GeometryIndex() + PrimitiveIndex());
    float3 hashColour = float3(float(mhash & 255), float((mhash >> 8) & 255), float((mhash >> 16) & 255)) / 255.0;
    payload.color = hashColour;
}

[shader("miss")]
void MyMissShader(inout RayPayload payload)
{
    payload.color = float3(0.1f, 0.1f, 0.1f);
}
